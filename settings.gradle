// @formatter:off
pluginManagement {
    plugins {
        id 'org.flywaydb.flyway'             version settings['version.flyway']
        id 'org.springframework.boot'        version settings['version.springBoot']
        id 'io.spring.dependency-management' version settings['version.springDependencyManagement']
        id 'com.github.node-gradle.node'     version settings['version.nodeGradlePlugin']
        id 'io.freefair.lombok'              version settings['version.lombok']
    }
}

rootProject.name = 'simple-todo-app'

include ':app-client'
include ':app-server'

enableFeaturePreview 'VERSION_CATALOGS'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // dependency version should be declared in 'gradle.properties' with 'version.' prefix
            this.properties
                .findAll { it.key.startsWith 'version.' }
                .collectEntries { [it.key - 'version.', it.value] }
                .each { version (it.key, it.value) }
        }
    }
}
// @formatter:on
